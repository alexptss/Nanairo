/*!
  \file NShapeTypeSettingBlock.qml
  \author Sho Ikeda

  Copyright (c) 2015-2016 Sho Ikeda
  This software is released under the MIT License.
  http://opensource.org/licenses/mit-license.php
  */

import QtQuick @qt_quick_version@
import QtQuick.Controls @qt_quick_controls_version@
import "../../SettingBlockItems"

NSettingBlock {
  id: shape_type_setting_block

  property url objectFilePath: Qt.resolvedUrl("")
  property var currentItem: null
  property bool isItemChangeMode: false

  labelText: qsTr("ShapeType")

  NComboBox {
    id: shape_type_combobox

    y: shape_type_setting_block.labelHeight + @item_offset@
    backgroundColor: shape_type_setting_block.color

    model: ["@planeObject@",
            "@meshObject@"]

    onCurrentIndexChanged: setCurrentItemValue("@shapeType@", currentText)
  }

  NButton {
    id: open_object_button

    property string objectFileName: ""

    y: (shape_type_combobox.y + shape_type_combobox.height) + @item_offset@
    width: 120
    enabled: shape_type_combobox.currentText == "@meshObject@"
    backgroundColor: shape_type_setting_block.color

    text: (objectFileName == "") ? qsTr("Open object file") : objectFileName

    NFileDialog {
      id: open_object_dialog
      title: qsTr("Open object file")
      nameFilters: ["Wavefront file (*.obj)"]
      onAccepted: shape_type_setting_block.objectFilePath = fileUrl
    }

    onEnabledChanged: {
      if (!enabled) {
        objectFilePath = Qt.resolvedUrl("");
      }
    }

    onClicked: {
      open_object_dialog.openDialog()
    }
  }

  onObjectFilePathChanged: {
    var filePath = objectFilePath.toString();
    if (filePath != "") {
      setCurrentItemValue("@objectFilePath@", filePath);
    }
    open_object_button.objectFileName = nanairoManager.getFileName(objectFilePath);
  }

  function initializeItem(item) {
    item["@shapeType@"] = shape_type_combobox.textAt(0);
    item["@objectFilePath@"] = "";
  }

  function reflectItem() {
    var item = currentItem;
    // Shape type
    var type = item["@shapeType@"];
    var typeIndex = shape_type_combobox.find(type);
    console.assert(typeIndex != -1, "Shape type isn't found: ", type);
    shape_type_combobox.currentIndex = typeIndex;
    // Object button
    if (type == "@meshObject@") {
      objectFilePath = Qt.resolvedUrl(item["@objectFilePath@"]);
    }
  }

  function setCurrentItem(item, itemChangeMode) {
    currentItem = item;
    isItemChangeMode = itemChangeMode;
  }

  function setCurrentItemValue(key, value) {
    if (currentItem != null) {
      currentItem[key] = value;
    }
  }

  function getItemData(item) {
    var itemData = {};
    var type = item["@shapeType@"];
    itemData["@shapeType@"] = type;
    if (type == "@meshObject@") {
      var filePath = Qt.resolvedUrl(item["@objectFilePath@"]);
      itemData["@objectFilePath@"] = nanairoManager.toRelativeFilePath(filePath);
    }
    return itemData;
  }

  function makeItem(itemData) {
    var item = {};
    var type = itemData["@shapeType@"];
    item["@shapeType@"] = type;
    if (type == "@meshObject@") {
      var filePath = nanairoManager.toAbsoluteFileUrl(itemData["@objectFilePath@"]);
      item["@objectFilePath@"] = filePath.toString();
    }
    return item;
  }
}
