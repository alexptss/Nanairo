/*!
  \file NGeometryTypeSettingBlock.qml
  \author Sho Ikeda

  Copyright (c) 2015-2016 Sho Ikeda
  This software is released under the MIT License.
  http://opensource.org/licenses/mit-license.php
  */

import QtQuick @qt_quick_version@
import QtQuick.Controls @qt_quick_controls_version@
import "../../SettingBlockItems"

NSettingBlock {
  id: geometry_type_setting_block

  property url objectFilePath: Qt.resolvedUrl("")
  property var currentItem: null
  property bool isItemChangeMode: false

  labelText: qsTr("GeometryType")

  NComboBox {
    id: geometry_type_combobox

    y: geometry_type_setting_block.labelHeight + @item_offset@
    backgroundColor: geometry_type_setting_block.color

    model: ["@planeObject@",
            "@meshObject@"]

    onCurrentIndexChanged: setCurrentItemValue("@geometryType@", currentText)
  }

  NButton {
    id: open_object_button

    property string objectFileName: ""

    y: (geometry_type_combobox.y + geometry_type_combobox.height) + @item_offset@
    width: 120
    enabled: geometry_type_combobox.currentText == "@meshObject@"
    backgroundColor: geometry_type_setting_block.color

    text: (objectFileName == "") ? qsTr("Open object file") : objectFileName

    NFileDialog {
      id: open_object_dialog
      title: qsTr("Open object file")
      nameFilters: ["Wavefront file (*.obj)"]
      onAccepted: geometry_type_setting_block.objectFilePath = fileUrl
    }

    onEnabledChanged: {
      if (!enabled) {
        objectFilePath = Qt.resolvedUrl("");
      }
    }

    onClicked: {
      open_object_dialog.openDialog()
    }
  }

  onObjectFilePathChanged: {
    var filePath = objectFilePath.toString();
    if (filePath != "") {
      setCurrentItemValue("@objectFilePath@", filePath);
    }
    open_object_button.objectFileName = nanairoManager.getFileName(objectFilePath);
  }

  function initializeItem(item) {
    item["@geometryType@"] = geometry_type_combobox.textAt(0);
    item["@objectFilePath@"] = "";
  }

  function reflectItem() {
    var item = currentItem;
    // Geometry type
    var type = item["@geometryType@"];
    var typeIndex = geometry_type_combobox.find(type);
    console.assert(typeIndex != -1, "Geometry type isn't found: ", type);
    geometry_type_combobox.currentIndex = typeIndex;
    // Object button
    if (type == "@meshObject@") {
      objectFilePath = Qt.resolvedUrl(item["@objectFilePath@"]);
    }
  }

  function setCurrentItem(item, itemChangeMode) {
    currentItem = item;
    isItemChangeMode = itemChangeMode;
  }

  function setCurrentItemValue(key, value) {
    if (currentItem != null) {
      currentItem[key] = value;
    }
  }

  function getItemData(item) {
    var itemData = {};
    var type = item["@geometryType@"];
    itemData["@geometryType@"] = type;
    if (type == "@meshObject@") {
      var filePath = Qt.resolvedUrl(item["@objectFilePath@"]);
      itemData["@objectFilePath@"] = nanairoManager.toRelativeFilePath(filePath);
    }
    return itemData;
  }

  function makeItem(itemData) {
    var item = {};
    var type = itemData["@geometryType@"];
    item["@geometryType@"] = type;
    if (type == "@meshObject@") {
      var filePath = nanairoManager.toAbsoluteFileUrl(itemData["@objectFilePath@"]);
      item["@objectFilePath@"] = filePath.toString();
    }
    return item;
  }
}
